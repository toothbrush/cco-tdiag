imports
{
import CCO.Printing
}

-------------------------------------------------------------------------------
-- Pretty printing
-------------------------------------------------------------------------------

attr Object Command Commands Picture
  syn   pp :: Doc

sem Object
  | Line     lhs.pp = ppCall "line"     @slope (showable @len)
  | Makebox  lhs.pp = ppCall "makebox"  @dim   (text @body)
  | Framebox lhs.pp = ppCall "framebox" @dim   (text @body)

sem Command
  | Put      lhs.pp = ppCall "put" @pos @obj.pp

sem Commands
  | Nil      lhs.pp = empty
  | Cons     lhs.pp = @hd.pp >-< @tl.pp

sem Picture
  | Picture  lhs.pp = text "\\begin{picture}" >|< ppPair @dim >-<
                      indent 2 @cmds.pp >-<
                      text "\\end{picture}"

-------------------------------------------------------------------------------
-- Utilities
-------------------------------------------------------------------------------

{
ppCall :: Show a => String -> (a, a) -> Doc -> Doc
ppCall cmd args body = singleLine >//< multiLine
  where
    prefix     = text ('\\' : cmd) >|< ppPair args
    singleLine = prefix >|<  braces body
    multiLine  = prefix >|< lbrace >|< text "%" >-< indent 2 body >|< rbrace

ppPair :: Show a => (a, a) -> Doc
ppPair (i, j) = parens (showable i >|< comma >|< showable j)
}